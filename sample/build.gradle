buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        mavenLocal()
    }
    dependencies {
        classpath 'com.github.kxfeng:migrate-android-res:1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.kxfeng.migrate-android-res'

migrateAndroidRes {
    layout_zh_task {
        from "layout-zh"
        to "layout-zh-rCN", "layout-zh-rTW"
    }

    values_zh_task {
        from "values-zh"
        to "values-zh-rCN", "values-zh-rTW"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.github.kxfeng.sample"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "zh", "zh-rCN", "zh-rTW", "zh-rHK", "zh-rMO", "zh-rSG"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "market", "minApi"

    productFlavors {
        googlePlay {
            dimension "market"
        }
        otherMarket {
            dimension "market"
        }

        minApi19 {
            dimension "minApi"
        }
        minApi23 {
            dimension "minApi"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    testCompile 'junit:junit:4.12'
}


